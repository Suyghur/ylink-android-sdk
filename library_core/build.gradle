plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.protobuf'
    id 'kotlin-kapt'
}

android {
    compileSdk rootProject.ext.android.compileSdk
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    ndkVersion rootProject.ext.android.ndkVersion

    defaultConfig {
        minSdk rootProject.ext.android.minSdk
        targetSdk rootProject.ext.android.targetSdk
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig false
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        kotlinOptions.freeCompilerArgs += ['-module-name', "cn.yyxx.ylink.core"]
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'

    implementation 'io.github.suyghur.dolin:zap:1.0.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'com.github.liujingxing.rxhttp:rxhttp:2.8.8'
    kapt 'com.github.liujingxing.rxhttp:rxhttp-compiler:2.8.8'

    //gRPC dependencies
    implementation "io.grpc:grpc-android:$rootProject.grpc_version"
    implementation "io.grpc:grpc-okhttp:$rootProject.grpc_version"
    implementation "io.grpc:grpc-stub:$rootProject.grpc_version"

    //stub-android
    implementation "io.grpc:grpc-protobuf-lite:$rootProject.grpc_version"
    implementation "io.grpc:grpc-kotlin-stub:$rootProject.grpc_kotlin_version"
    implementation "com.google.protobuf:protobuf-kotlin-lite:$rootProject.protobuf_version"
}

kapt {
    arguments {
        arg("rxhttp_package", "cn.yyxx.ylink.core.network.rxhttp")  //指定RxHttp类包名，可随意指定
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$rootProject.protobuf_version"
    }
    plugins {
        java {
            artifact = "io.grpc:protoc-gen-grpc-java:$rootProject.grpc_version"
        }
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$rootProject.grpc_version"
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:$rootProject.grpc_kotlin_version:jdk8@jar"
        }
    }
    generateProtoTasks {
        all().forEach {
            it.plugins {
                java {
                    option "lite"
                }
                grpc {
                    option "lite"
                }
                grpckt {
                    option "lite"
                }
            }
            it.builtins {
                kotlin {
                    option "lite"
                }
            }
        }
    }
}